plugins {
    id "org.ajoberstar.grgit" version "3.0.0"
    id "org.springframework.boot" version "2.1.8.RELEASE" apply false
    id "io.spring.dependency-management" version "1.0.8.RELEASE" apply false
}

repositories {
    mavenCentral()
}

subprojects {
    apply plugin: 'java' // All sub-projects are currently publishing jar files.
    apply plugin: 'maven-publish'
    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
        }
    }

    group 'com.learning.rainbow'
    version = "${grgit.head().id}"
    sourceCompatibility = JavaVersion.VERSION_1_8

    repositories {
        mavenCentral()
    }

    configurations {
        all {
            // Excluding spring default logging implementation to include log4j2
            exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
        }
    }

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter'
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
    }
}